# Problem 84 Solution

```elm
module Main exposing (..)

import Html
import List
import Set


type alias Edge comparable =
    ( comparable, comparable, Int )


type alias AdjList comparable =
    List ( comparable, List comparable )


type alias Graph comparable =
    ( List comparable, List (Edge comparable) )


type alias SpanningTree comparable =
    ( Graph comparable, Graph comparable )



-- Given a starting node and a graph with weighted edges,
-- return the minimum spanning tree in a graph


prim : comparable -> Graph comparable -> Graph comparable
prim start ( nodes, edges ) =
    -- should remove parallel edges and loops
    snd <| findMst ( ( nodes, edges ), ( [ start ], [] ) )



-- given a SpanningTree, add the lowest weight edge from the left graph
-- to the right graph until it includes all nodes


findMst : SpanningTree comparable -> SpanningTree comparable
findMst ( ( lNodes, lEdges ), ( rNodes, rEdges ) ) =
    let
        edges =
            List.filter (hasOneNodeIn rNodes) lEdges

        edge =
            minimumBy (\( a, b, w ) -> w) edges
    in
        case edge of
            Nothing ->
                ( ( lNodes, lEdges ), ( rNodes, rEdges ) )

            Just e ->
                let
                    rEdges' =
                        rEdges ++ [ e ]

                    rNodes' =
                        nodesOfEdges rEdges'

                    lEdges' =
                        Set.toList <| Set.remove e <| Set.fromList lEdges
                in
                    if List.length lNodes <= List.length rNodes then
                        ( ( lNodes, lEdges ), ( rNodes', rEdges' ) )
                    else
                        findMst ( ( lNodes, lEdges' ), ( rNodes', rEdges' ) )


hasOneNodeIn : List comparable -> Edge comparable -> Bool
hasOneNodeIn nodes ( a, b, w ) =
    (List.member a nodes && (not (List.member b nodes)))
        || ((not (List.member a nodes)) && List.member b nodes)


nodesOfEdges : List (Edge comparable) -> List comparable
nodesOfEdges edges =
    List.sort
        <| Set.toList
        <| Set.fromList
        <| List.concatMap (\( a, b, w ) -> [ a, b ]) edges



-- borrowed from List.Extra


{-| Find the first minimum element in a list using a comparable transformation
-}
minimumBy : (a -> comparable) -> List a -> Maybe a
minimumBy f ls =
    let
        minBy x ( y, fy ) =
            let
                fx =
                    f x
            in
                if fx < fy then
                    ( x, fx )
                else
                    ( y, fy )
    in
        case ls of
            [ l' ] ->
                Just l'

            l' :: ls' ->
                Just <| fst <| List.foldl minBy ( l', f l' ) ls'

            _ ->
                Nothing


last : List a -> Maybe a
last list =
    List.head (List.reverse list)


main : Html.Html a
main =
    Html.text
        <| case test of
            0 ->
                "Your implementation passed all tests."

            1 ->
                "Your implementation failed one test."

            x ->
                "Your implementation failed " ++ (toString x) ++ " tests."


test : Int
test =
    List.length
        <| List.filter ((==) False)
            [ prim 'f' graph84
                == ( [ 'b', 'c', 'f', 'g', 'h', 'k' ]
                   , [ ( 'b', 'f', 8 )
                     , ( 'b', 'c', 5 )
                     , ( 'b', 'g', 9 )
                     , ( 'g', 'h', 15 )
                     , ( 'f', 'k', 17 )
                     ]
                   )
            ]


graph80 =
    ( [ 'b', 'c', 'd', 'f', 'g', 'h', 'k' ]
    , [ ( 'b', 'c', 5 ), ( 'b', 'f', 8 ), ( 'c', 'f', 9 ), ( 'f', 'k', 17 ), ( 'g', 'h', 15 ) ]
    )


graph84 =
    ( [ 'b', 'c', 'd', 'f', 'g', 'h', 'k' ]
    , [ ( 'b', 'c', 5 )
      , ( 'b', 'f', 8 )
      , ( 'b', 'g', 9 )
      , ( 'c', 'f', 9 )
      , ( 'f', 'g', 13 )
      , ( 'f', 'k', 17 )
      , ( 'g', 'h', 15 )
      ]
    )

```
[Back to problem](../p/p84.md)

